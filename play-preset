#!/usr/bin/env perl
use strict;
use warnings;

use List::MoreUtils qw(firstidx);
use IO::Socket::INET;

my $sock = IO::Socket::INET->new('soundbridge.local:5555')
    or die "Unable to bind to soundbridge: $!";

die "Huh?" unless $sock->getline =~ /roku: ready/;

sub cmd ($$) {
    my $cmd  = shift;
    my $code = shift;
    my $pre  = (split / /, $cmd, 2)[0];

    $sock->print("$cmd\n");
    while ($_ = $sock->getline) {
        s/^\Q$pre\E:\s*//;
        s/[\r\n]//g;
        $code->($_);
        last if /^(?:ListResultEnd|OK|.*Error)/;
    }
}
sub parse (&) { $_[0] }

my @presets;
cmd 'ListPresets' => parse {
    return if /^ListResult/;
    return unless /\S/;
    push @presets, $_;
};

if (my $play = join ' ', @ARGV) {
    my $index = $play =~ /\D/
                    ? firstidx { /$play/i } @presets
                    : $play - 1;
    {
        local $| = 1;
        print "Playing $presets[$index]... ";
    }
    cmd "PlayPreset $index" => parse {
        print "$_\n";
    };
} else {
    print $_+1, ". $presets[$_]\n" for 0..$#presets;
}

$sock->close;

